package com.example.practica1.parserLexer.parser;

import com.example.practica1.parserLexer.lexer.LexerP1;
import com.example.practica1.parserLexer.chartCode.*;
import com.example.practica1.parserLexer.attributes.*;
import com.example.practica1.parserLexer.*;
import com.example.practica1.parserLexer.Errors.*;
import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.HashMap;
import java.util.List;

parser code {:
    public ParserP1(LexerP1 lexer){
        super(lexer);
    }


    public void syntax_error(Symbol cur_token) {
        HashMap<String, String> symbolNames = Validate.getSymbolNames();
		List<Integer> tokens = expected_token_ids();
        int line = cur_token.left;
        int column = cur_token.right;
        String lexeme = cur_token.value.toString();
		int type = Errors.SINTAX;
        String des = "Se esperaba: ";
		for(Integer i : tokens) {	
            String fromId = symbl_name_from_id(i);
            String symbolName = symbolNames.get(fromId);
            if(symbolName != null){
                des += "'"+symbolName+ "' o ";
            }
            else{
                des += fromId +" o ";
            }			
		}
        des = des.substring(0, des.length() - 3);
        Errors.getErrors().addLS(line, column, des, lexeme, type);
	}

    public void report_fatal_error(String message, Object info) {
		System.out.println("message: " + message);
		System.out.println("info: " + info);
	}

    protected int error_sync_size() {
        return 1;
    }
:}


terminal SEMICOLON, COLON, COMMA;
terminal LBRACE, RBRACE;
terminal LBRACKET, RBRACKET;
terminal LPAREN, RPAREN;
terminal String STRING;

terminal PLUS, MINUS, DIVISION, TIMES;
terminal Double INTEGER, DECIMAL;
// Graphs definition
terminal DEF, BARRAS, PIE;
terminal TITULO, EJEX, EJEY, UNIR;
terminal ETIQUETAS, VALORES, TIPO, EXTRA, TOTAL;
terminal String CANTIDAD, PORCENTAJE;
terminal ERROR;

terminal EJECUTAR;


non terminal s, fin;
non terminal ChartCode chart;

non terminal ArrayList<Attribute> cuerpo_barras; 
non terminal Attribute statement_barras; 
non terminal ArrayList<Attribute> cuerpo_pie;
non terminal Attribute statement_pie;

non terminal Attribute<String> titulo;
non terminal Attribute<ArrayNode<String>> ejex;
non terminal Attribute<ArrayNode<Operation>> ejey;
non terminal Attribute<ArrayNode<Operation[]>> unir;
non terminal Attribute<ArrayNode<String>> etiquetas;
non terminal Attribute<ArrayNode<Operation>> valores;
non terminal Attribute<String> tipo;
non terminal Attribute<Operation> total;
non terminal Attribute<String> extra;
non terminal String execute;

non terminal String pie_types;

non terminal ArrayNode<String>array_strings;
non terminal ArrayNode<String>strings_comma;
non terminal ArrayNode<Operation> array_operations;
non terminal ArrayNode<Operation> operations_comma;
non terminal ArrayNode<Operation[]> tuplas_comma;
non terminal Operation[] tupla;

non terminal Operation operation;
non terminal Operation factor;
non terminal Operation term;







s::= 
                        chart:cht s
                        {:
                            if(cht != null) Execute.getExecute().addChart(cht);
                        :}
                        |execute:ex s
                        {:
                            if(ex != null) Execute.getExecute().addExecuteCall(ex);
                        :}
                        | chart:cht
                        {:
                            if(cht != null) Execute.getExecute().addChart(cht);
                        :}
                        | execute:ex
                        {:
                            if(ex != null) Execute.getExecute().addExecuteCall(ex);
                        :}                        
                        ;

fin::= 
                        SEMICOLON                        
                        ;

chart::= 
                        DEF BARRAS LBRACE cuerpo_barras:arr RBRACE
                        {:
                            BarChartCode chart = Validate.validateBar(arr);
                            RESULT = chart;
                        :}
                        |DEF PIE LBRACE cuerpo_pie:arr RBRACE
                        {:
                            PieChartCode chart = Validate.validatePie(arr);
                            RESULT = chart;
                        :}
                        |DEF BARRAS LBRACE error RBRACE
                        |DEF PIE LBRACE error RBRACE
                        ;

cuerpo_barras::= 
                        statement_barras:att fin cuerpo_barras:arr
                        {:
                            arr.add(att);
                            RESULT = arr;
                        :}
                        |statement_barras:att fin
                        {:
                            ArrayList<Attribute> attributes = new ArrayList<>();
                            attributes.add(att);
                            RESULT = attributes;
                        :}
                        |statement_barras error cuerpo_barras
                        |statement_barras error                                      
                        ;
statement_barras::= 
                        titulo:ti  
                        {:
                            RESULT = ti;
                        :}
                        |ejex:ex  
                        {:
                            RESULT = ex;
                        :}
                        |ejey :ey 
                        {:
                            RESULT = ey;
                        :}
                        |unir:u  
                        {:
                            RESULT = u;
                        :}                                              
                        ;

titulo::= 
                        TITULO COLON STRING:st
                        {:
                          st = st.replaceAll("\"", "");
                          Attribute<String> att = new Attribute<>(st, Attribute.TITULO, stleft);
                          RESULT = att;
                        :}
                        | TITULO COLON error
                        ;
ejex::= 
                        EJEX COLON array_strings:arr
                        {:
                            Attribute<ArrayNode<String>> att = new Attribute<>(arr, Attribute.EJEX, arrleft);
                            RESULT = att;
                        :}
                        ;

array_strings::=
                        LBRACKET strings_comma:arr RBRACKET
                        {:
                            RESULT = arr;
                        :}
                        ;
strings_comma::= 
                        STRING:st COMMA strings_comma:nexts
                        {:
                            st = st.replaceAll("\"", "");
                            ArrayNode<String> node = new ArrayNode<String>(st);
                            node.setNext(nexts);
                            RESULT = node;
                        :}
                        |STRING:st
                        {:
                            st = st.replaceAll("\"", "");
                            ArrayNode<String> node = new ArrayNode<String>(st);                            
                            RESULT = node;
                        :}
                        ;
ejey::=
                        EJEY COLON array_operations:arr
                        {: 
                            Attribute<ArrayNode<Operation>> att = new Attribute<>(arr, Attribute.EJEY, arrleft);
                            RESULT = att;
                        :}
                        | EJEY COLON error      
                        ;

array_operations::=         
                        LBRACKET operations_comma:arr RBRACKET
                        {:
                            RESULT = arr;
                        :}
                        ;
operations_comma::=
                        operation:o COMMA operations_comma:nexts
                        {: 
                            ArrayNode<Operation> node = new ArrayNode<Operation>(o);
                            node.setNext(nexts);
                            RESULT = node;
                        :}
                        | operation: o
                        {: 
                            ArrayNode<Operation> node = new ArrayNode<Operation>(o);
                            RESULT = node;
                        :}                        
                        ;                        
unir::=
                        UNIR COLON LBRACKET tuplas_comma:tuplas RBRACKET
                        {:
                           Attribute<ArrayNode<Operation[]>> att = new Attribute<>(tuplas, Attribute.UNIR, tuplasleft);
                           RESULT = att;                           
                        :}
                        | UNIR COLON error
                        ;
tuplas_comma::= 
                        tupla:tp COMMA tuplas_comma:prevs
                        {:
                            ArrayNode<Operation[]> node = new ArrayNode<Operation[]>(tp);
                            node.setNext(prevs);
                            RESULT = node;
                        :}
                        | tupla:tp
                        {:
                            ArrayNode<Operation[]> node = new ArrayNode<Operation[]>(tp);                            
                            RESULT = node;
                        :}
                        | tupla error tuplas_comma
                        ;
tupla::=
                        LBRACE operation:x COMMA operation:y RBRACE
                        {: 
                            Operation[] array = new Operation[2];
                            array[0] = x;
                            array[1] = y;
                            RESULT = array;
                        :}                        
                        ;
                        
operation::=
                        operation:o PLUS:sm term:t
                        {: 
                            Execute.getExecute().addMathSymbol(smleft, smright, MathSymbol.SUMA);
                            RESULT=new Operation("+", o, t);
                        :}
                        |operation:o MINUS:sm term:t
                        {: 
                            Execute.getExecute().addMathSymbol(smleft, smright, MathSymbol.RESTA);
                            RESULT=new Operation("-", o, t);
                        :}
                        |term:t 
                        {:RESULT=t;:}
                        ;

term::=
                        term:t TIMES:sm factor:f
                        {: 
                            Execute.getExecute().addMathSymbol(smleft, smright, MathSymbol.MULTIPLICACION);
                            RESULT=new Operation("*", t, f);
                        :}
                        |term:t DIVISION:sm factor:f
                        {: 
                            Execute.getExecute().addMathSymbol(smleft, smright, MathSymbol.DIVISION);
                            RESULT=new Operation("/", t, f);
                        :}
                        |factor:f 
                        {: RESULT=f;:}
                        ;
factor::=
                        INTEGER:val 
                        {: RESULT=new Operation("n", val);:}
                        |DECIMAL:val
                        {: RESULT=new Operation("n", val);:}
                        |LPAREN operation:op RPAREN
                        {: RESULT=op;:}
                        | MINUS:sm factor:op
                        {: 
                            Execute.getExecute().addMathSymbol(smleft, smright, MathSymbol.RESTA);
                            RESULT=new Operation("minus", op, null);
                        :}
                        ;

cuerpo_pie::=
                        statement_pie:att fin cuerpo_pie:arr
                        {:                           
                            arr.add(att);
                            RESULT = arr;
                        :}
                        | statement_pie:att fin
                        {:
                            ArrayList<Attribute> attributes = new ArrayList<>();
                            attributes.add(att);
                            RESULT = attributes;
                        :}
                        | statement_pie error cuerpo_pie
                        | statement_pie error  
                        ;
statement_pie::=        
                        titulo:ti
                        {:
                            RESULT = ti;
                        :}
                        |etiquetas:et
                        {:
                            RESULT = et;
                        :}
                        |valores:va
                        {:
                            RESULT = va;
                        :}
                        |unir:u
                        {:
                            RESULT = u;
                        :}
                        |tipo:tp
                        {:
                            RESULT = tp;
                        :}
                        |total:to
                        {:
                            RESULT = to;
                        :} 
                        |extra:ex 
                        {:
                            RESULT = ex;
                        :}                        
                        ;                        
etiquetas::=
                        ETIQUETAS COLON array_strings:arr
                        {:
                            Attribute<ArrayNode<String>> att = new Attribute<>(arr, Attribute.ETIQUETAS, arrleft);
                            RESULT = att;
                        :}
                        | ETIQUETAS COLON error
                        ;
valores::=
                        VALORES COLON array_operations:arr
                        {:
                            Attribute<ArrayNode<Operation>> att = new Attribute<>(arr, Attribute.VALORES, arrleft);
                            RESULT = att;
                        :}
                        | VALORES COLON error
                        ;
tipo::=
                        TIPO COLON pie_types:st
                        {:
                            Attribute<String> att = new Attribute<>(st, Attribute.TIPO, stleft);
                            RESULT = att;
                        :}
                        | TIPO COLON error
                        ;
pie_types::=
                        CANTIDAD:st
                        {:
                            RESULT = st;
                        :}
                        |PORCENTAJE:st
                        {:
                            RESULT = st;
                        :}
                        ;
total::=
                        TOTAL COLON operation:op
                        {:
                            Attribute<Operation> att = new Attribute<>(op, Attribute.TOTAL, opleft);
                            RESULT = att;
                        :}
                        | TOTAL COLON error
                        ;
extra::=
                        EXTRA COLON STRING:st
                        {:
                            st = st.replaceAll("\"", "");
                            Attribute<String> att = new Attribute<>(st, Attribute.EXTRA, stleft);
                            RESULT = att;
                        :}
                        | EXTRA COLON error
                        ;
execute::=
                        EJECUTAR LPAREN STRING:st RPAREN fin
                        {:
                            st = st.replaceAll("\"", "");                            
                            RESULT = st;
                        :}
                        |EJECUTAR LPAREN error RPAREN fin
                        |EJECUTAR LPAREN STRING RPAREN error
                        ;